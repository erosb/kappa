import java.nio.charset.StandardCharsets

plugins {
  id "java"
  id "org.sonarqube" version "2.8"
  id 'jacoco'
  id 'maven-publish'
  id 'signing'
  id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'

  group = "com.github.erosb"
  version = "2.0.0-RC5"
  description = "Forked version of openapi4j aiming for openapi 3.1 support"

  java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }
  compileJava.options.encoding = StandardCharsets.UTF_8.toString()

  tasks.withType(JavaCompile) {
    options.encoding = StandardCharsets.UTF_8.toString()
  }

  repositories {
    mavenCentral()
    mavenLocal()
  }

  jar {
    manifest {
      attributes 'Build-date': new Date()
      attributes 'Implementation-Title': project.name
      attributes 'Implementation-Version': project.version
    }
  }
}

sonarqube {
  properties {
    property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      logger.warn(project.group + " : " + project.name + " : " + project.version)
      logger.warn(project.description)

      groupId = project.group
      artifactId = project.name
      version = project.version

      from components.java
      pom {
        name = project.name
        description = project.description
        url = "https://github.com/erosb/openapi4j"
        licenses {
          license {
            name = "Apache License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
        developers {
          developer {
            id = 'erosb'
            name = 'Bence Eros'
          }
        }
        scm {
          connection = "scm:git:git://github.com:erosb/openapi4j.git"
          developerConnection = "scm:git:https://github.com/erosb/openapi4j"
          url = "https://github.com/erosb/openapi4j"
        }
      }
    }
  }
}

tasks.withType(Sign) {
  onlyIf { !version.endsWith("SNAPSHOT") }
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenJava
}


nexusPublishing {
  repositories {
    sonatype {
      username = System.getenv("publishUsername")
      password = System.getenv("publishPassword")
    }
  }
}
